apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

signing {
    sign configurations.archives
}

def lib_version = '1.1.0'
def user_name = 'wandersnail'
def lib_group = 'cn.wandersnail'
def lib_artifact = 'widget-generic'
def lib_packaging = 'aar'
def lib_description = 'Some customized views commonly used'
def lib_website = 'https://github.com/wandersnail/widget-generic'

group = lib_group
version = lib_version
archivesBaseName = "widget-generic"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "$lib_version"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compileOnly "androidx.appcompat:appcompat:$appcompat_version"
    compileOnly "androidx.recyclerview:recyclerview:$recyclerview_version"
    compileOnly "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    setClasspath(classpath + project.files(android.getBootClasspath().join(File.pathSeparator)))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name lib_artifact
                    artifactId lib_artifact
                    packaging lib_packaging
                    description lib_description
                    version lib_version
                    url lib_website

                    scm {
                        url lib_website
                        connection "scm:git:${lib_website}.git"
                        developerConnection "scm:git:${lib_website}.git"
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id user_name
                            name 'Zeng Fansheng'
                            email 'bingmo977@qq.com'
                        }
                    }
                }
            }
        }
    }
}