apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'maven'

def lib_version = '1.2.1'
group = "com.github.wandersnail"
version = lib_version

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "$lib_version"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
}

publish {
    repoName="android"//仓库名
    userOrg = 'wandersnail' //bintray.com用户名  
    groupId = 'com.github.wandersnail' //jcenter上的路径  
    artifactId = 'widgets' //项目名称  
    publishVersion = "$lib_version" //版本号  
    desc = '自定义控件'
    website = 'https://github.com/wandersnail'
}

if (project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }) {
    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find {
            it.path == ":$project.name:generatePomFileForReleasePublication"
        }.doLast {
            file("build/publications/release/pom-default.xml").delete()
            println 'Overriding pom-file to make sure we can sync to maven central!'
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name "widgets"
                    artifactId 'widgets'
                    packaging 'aar'
                    description 'Android自定义控件库'
                    url 'https://github.com/wandersnail'
                    version lib_version

                    scm {
                        url 'https://github.com/wandersnail/widgets'
                        connection 'scm:git:https://github.com/wandersnail/widgets.git'
                        developerConnection 'scm:git:https://github.com/wandersnail/widgets.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'wandersnail'
                            name 'Zeng Fansheng'
                            email 'bingmo977@qq.com'
                        }
                    }
                }
            }.writeTo("build/publications/release/pom-default.xml")
        }
    }
}